# This file is part of the Fuzion language implementation.
#
# The Fuzion language implementation is free software: you can redistribute it
# and/or modify it under the terms of the GNU General Public License as published
# by the Free Software Foundation, version 3 of the License.
#
# The Fuzion language implementation is distributed in the hope that it will be
# useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public
# License for more details.
#
# You should have received a copy of the GNU General Public License along with The
# Fuzion language implementation.  If not, see <https://www.gnu.org/licenses/>.


# -----------------------------------------------------------------------
#
#  Tokiwa Software GmbH, Germany
#
#  Source code of Fuzion standard library feature exit
#
#  Author: Fridtjof Siebert (siebert@tokiwa.software)
#
# -----------------------------------------------------------------------

# exit -- effect that terminates a computation in exit
#
exit (

  # the handler this effect uses to exit
  p Exit_Handler,

  redef r effectMode.val,

  _ unit
  ) : effect r
is

  # exit with the given code
  #
  exit(code i32) => p.exit code


# exit with no argument returns exit.env, the currently installed
# exit handler.
#
exit =>
  exit_type.installDefault
  exit.env


# exit with a code argument calls exit.exit code, i.e., it uses the
# current exit efffect to exit with the given message.
#
exit(code i32) => exit.exit code


# install given exit handler and run code with it.
#
exit(handler Exit_Handler, code ()->unit) =>
  exit handler (effectMode.inst code) unit


# Exit_Handler -- abstract exit
#
Exit_Handler ref is

  # exit with the given message
  #
  exit(code i32) void is abstract


# type related to exit declaring features not requiring an instance of exit
#
exit_type is

  installDefault is
    if !effects.exists exit
      _ := exit defaultExitHandler effectMode.default unit

  # default exit handler using io.err and exit with return code 1
  #
  defaultExitHandler : Exit_Handler is

    # exit with the given message and exit with return code 1
    #
    exit(code i32) =>
      fuzion.std.exit code


# delayed_exit -- effect that runs some code that can set an exit code, but
# only exits after all the code has been run
#
# this is intended for use in tests
#
delayed_exit (_ unit) : effect effectMode.plain is


  # mutable value of the current exit code set
  #
  exit_code := mut 0


  # update the exit code to the given value
  #
  set0(code i32) =>
    exit_code <- code


  # execute the code given by f. if after f is completed, the exit code is
  # not 0, the program will exit with the given exit code. otherwise, the
  # value returned by f is returned.
  #
  go(T type, f ()->T) T is
    fo () -> option T is ()->f()
    r := run fo (unit)->nil
    match r
      nil => panic "*** unexpected abort in {delayed_exit.this.type}"
      v T =>
        if exit_code.get != 0
          exit exit_code.get
        else
          v


  type.install_default =>
    (delayed_exit unit).default


# short-hand access to the delayed_exit effect
#
delayed_exit =>
  delayed_exit.type.install_default
  delayed_exit.env
